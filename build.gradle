buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url  "http://dl.bintray.com/kaleidos/maven"
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'org.codehaus.groovy:gradle-groovy-android-plugin:0.3.8'
        classpath 'net.kaleidos.gradle.plugins:emerger:0.0.2' // Emerger
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'groovyx.grooid.groovy-android'
apply plugin: 'net.kaleidos.gradle.plugins.emerger'
apply plugin: 'io.fabric'

android {

    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }

    compileSdkVersion 22
    buildToolsVersion "22.0.0"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 22


        // Enabling multidex support.
        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        exclude 'LICENSE.txt' // Needed by Spock/Android
        exclude 'META-INF/services/org.codehaus.groovy.transform.ASTTransformation'
        exclude 'META-INF/services/org.codehaus.groovy.runtime.ExtensionModule'
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    productFlavors {
        greach {
            applicationId "com.botconf.greach"
            versionName "1.0-greach"
            versionCode 2
        }

        ggx {
            applicationId "com.botconf.ggx"
            versionCode 5
            versionName "1.0-ggx"
        }
    }

    android.applicationVariants.all {  v ->
        if (v.buildType.name == "release") {
            v.outputs.each { output ->
                output.outputFile = new File(output.outputFile.parent,
                        output.outputFile.name.replace(".apk", "${v.versionCode}_${v.versionName}.apk"))
            }
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile 'com.android.support:multidex:1.0.0'

    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:design:22.2.1'
    compile 'com.android.support:recyclerview-v7:22.0.0'

    // Groovy version
    compile 'org.codehaus.groovy:groovy:2.4.5:grooid'

    // Groovy Json handling
    compile ('org.codehaus.groovy:groovy-json:2.4.0-rc-1') {
        transitive = false
    }

    // Getting rid of boiler plate code
    compile 'com.arasthel:swissknife:1.3.1'

    compile 'com.mcxiaoke.volley:library:1.0.16'

    // Spock/Android
    androidTestCompile "com.andrewreitz:spock-android:1.2.1"
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
        exclude group: 'junit'
    }
    // Optional, needed for mocking
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'

    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }

    compile('com.twitter.sdk.android:twitter:1.11.0@aar') {
        transitive = true;
    }

    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    //compile 'com.google.android.gms:play-services-maps:7.5.0'

    compile 'com.googlecode.plist:dd-plist:1.16'

}